plugins {
    id 'com.gradle.plugin-publish' version '2.0.0'
    id 'java-gradle-plugin'
}

group "io.smallrye"

compileJava.options.compilerArgs.addAll(['--release', '17'])

sourceSets {
    main.java.srcDirs = ['plugin/src/main/java']
    main.resources.srcDirs = ['plugin/src/main/resources']
}
compileJava {
    options.encoding = 'UTF-8'
    sourceCompatibility = '17'
    targetCompatibility = '17'
}

compileTestJava {
    options.encoding = 'UTF-8'
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    api gradleApi()
    implementation "io.smallrye:jandex:3.0.0"
    implementation "io.smallrye:smallrye-graphql:${version}"
    implementation "io.smallrye:smallrye-graphql-schema-builder:${version}"
    implementation "jakarta.json.bind:jakarta.json.bind-api:2.0.0"
    implementation "jakarta.validation:jakarta.validation-api:3.0.2"
    implementation "org.eclipse:yasson:2.0.4"
    implementation "org.jboss.logging:jboss-logging:3.6.1.Final"

    testImplementation gradleTestKit()
    testImplementation 'org.junit.jupiter:junit-jupiter:5.14.0'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher:1.14.0'
}

test {
    testLogging {
        events "passed", "skipped", "failed"
    }
    useJUnitPlatform()
    // multiple Gradle invocations executed from the same testing JVM cause problems, so fork
    // for each test, and make sure we only run one Gradle execution in each test
    forkEvery 1
}

javadoc {
    options.addStringOption('encoding', 'UTF-8')
}

gradlePlugin {
    plugins {
        smallryeGraphQLPlugin {
            id = 'io.smallrye.graphql'
            implementationClass = 'io.smallrye.graphql.gradle.SmallRyeGraphQLPlugin'

            website.set('https://github.com/smallrye/smallrye-graphql/')
            vcsUrl.set('https://github.com/smallrye/smallrye-graphql')
            tags.set(['smallrye', 'graphql', 'microprofile'])

            displayName = 'SmallRye: MicroProfile GraphQL Gradle Plugin'
            description = 'SmallRye: MicroProfile GraphQL Gradle Plugin'
        }
    }
}

publishing {
    publications {
        pluginMaven(MavenPublication) {
            groupId = project.group
        }
    }
}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
}
